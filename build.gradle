/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.taskdefs.condition.Os

apply from: "$rootDir/gradle/jsProject.gradle"

configurations {
    ballerinaTools
}


task installDeps() {
    doFirst {
        exec {
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', 'npm', 'install'
            } else {
                commandLine 'npm', 'install'
            }
        }
    }
}

task copyDeps(type: Copy) {
    dependsOn installDeps
    from "$rootDir/node_modules/@wso2-enterprise/ballerina-low-code-editor/build/"
    into "$rootDir/resources/jslibs"
    include "BLCEditor.js"
}

task copyConfigEditorDeps(type: Copy) {
    dependsOn installDeps
    from "$rootDir/node_modules/@wso2-enterprise/ballerina-configurable-editor/build/umd"
    into "$rootDir/resources/jslibs"
    include "ConfigEditor.js"
}

task copyPakoDeps(type: Copy) {
    dependsOn installDeps
    from "$rootDir/node_modules/pako/dist"
    into "$rootDir/resources/jslibs"
    include "pako.min.js"
}

npmBuild() {
    dependsOn ':bbe-viewer:buildBBEViewer'
    dependsOn ':perf-viewer:buildPerfViewer'
    dependsOn ':swagger-viewer:buildSwaggerViewer'
    dependsOn copyDeps
    dependsOn copyConfigEditorDeps
    PatternSet patternSet = new PatternSet();
    patternSet.exclude("**/node_modules/**");
    patternSet.exclude("**/dist/**");
    patternSet.exclude("**/out/**");
    patternSet.exclude("**/*.vsix/**");
    patternSet.exclude("**/coverage/**");

    FileTree src = files('.').asFileTree;
    inputs.files(src.matching(patternSet))  

    outputs.dir("out")
    outputs.dir("dist")
    outputs.cacheIf { true } 

    exec { commandLine 'git', 'submodule', 'update', '--init', '--recursive' }
    exec { commandLine 'git', 'submodule', 'foreach', 'git', 'pull', 'origin', 'master' }

    doFirst {
        exec {
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', 'npm', 'run', 'package'
            } else {
                commandLine 'npm', 'run', 'package'
            }
        }
    }
}

task assemble(overwrite: true) {
    dependsOn 'npmBuild'
}

task test(type: Exec) {
    // mustRunAfter ':examples-test:test'
    // dependsOn copyDistribution
    dependsOn assemble
    doFirst {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            commandLine 'cmd', '/c', 'npm', 'run', 'test'
        } else {
            commandLine 'npm', 'run', 'test'
        }
    }
}

check {
    dependsOn test
}
