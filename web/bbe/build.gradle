/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
 
import org.apache.tools.ant.taskdefs.condition.Os

apply from: "$rootDir/gradle/jsProject.gradle"

configurations {
    bbeviewer
}

task buildBBEViewer {
    PatternSet patternSet = new PatternSet();
	patternSet.exclude("**/node_modules/**");
    patternSet.exclude("**/build/**");
    patternSet.exclude("**/lib/**");
    patternSet.exclude("**/coverage/**");

    FileTree src = files('.').asFileTree;
    inputs.files(src.matching(patternSet))

    outputs.dir("build")
    outputs.cacheIf { true }
    doFirst {
        exec {
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', 'npm', 'run', 'build'
            } else {
                commandLine 'npm', 'run', 'build'
            }
        }
        copy {
            from './build'
            into './../../resources/jslibs'
            include 'BBEViewer.js'
            include 'themes/**'
            exclude 'themes/assets'
        }
    }
}

task testBBEViewer() {
    onlyIf {
        !assemble.state.upToDate
    }
    doFirst {
        exec {
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', 'npm', 'run', 'test'
            } else {
                commandLine 'npm', 'run', 'test'
            }
        }
    }
}

artifacts.add('bbeviewer', file("build/"))

check {
    dependsOn testBBEViewer
}